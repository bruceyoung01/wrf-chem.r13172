#!/bin/csh
#
# DART software - Copyright 2004 - 2013 UCAR. This open source software is
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# DART $Id$
#
# usage: mkmf_nogaps_to_dart [ -mpi | -nompi ]
#
# without any args, builds nogaps_to_dart without mpi libraries, and it will run
# as a normal executable. if -mpi is given, it will be compiled with the mpi
# libraries and can run with multiple cooperating processes.

#
# this assumes that in ../src is a set of compiled nogaps routines,
# archived into libnogaps.a, WITHOUT forecast.o since it contains a
# program statement (main).  to make it this lib:
#   ar crvf libnogaps.a *.o
#   ar dvf libnogaps.a forecast.o
#   ranlib libnogaps.a


if ( $#argv > 0 ) then
  if ("$argv[1]" == "-mpi") then
    setenv usingmpi 1
  else if ("$argv[1]" == "-nompi") then
    setenv usingmpi 0
  else
    echo "Unrecognized argument to mkmf_nogaps_to_dart: $argv[1]"
    echo "Usage: mkmf_nogaps_to_dart [ -mpi | -nompi ]"
    echo " default is to generate a Makefile without MPI support."
    exit -1
  endif
else
  setenv usingmpi 0
endif


# make a backup copy of the path_names file, and then use
# sed to make sure it includes either the non-mpi subroutines,
# or the subroutines which really call mpi.
cp --preserve=timestamps -f path_names_nogaps_to_dart path_names_nogaps_to_dart.back

if ( $usingmpi ) then

  echo "Making Makefile with MPI"
  touch using_mpi_for_nogaps_to_dart
  sed -e 's;/null_mpi_util;/mpi_util;' path_names_nogaps_to_dart.back >! path_names_nogaps_to_dart
  setenv wrapper_arg -w

else

  echo "Making Makefile without MPI"
  rm -f using_mpi_for_nogaps_to_dart
  sed -e 's;/mpi_util;/null_mpi_util;' path_names_nogaps_to_dart.back >! path_names_nogaps_to_dart
  setenv wrapper_arg ""

endif

# remove temp file and now really call mkmf to generate makefile
rm -f path_names_nogaps_to_dart.back

../../../mkmf/mkmf -p nogaps_to_dart -t ../../../mkmf/mkmf.template \
 -o "-I../src"  -l "-L../src -lnogaps"  -a "../../.." ${wrapper_arg} path_names_nogaps_to_dart

exit $status

# <next few lines under version control, do not edit>
# $URL$
# $Revision$
# $Date$

